group 'com.nullables'
version '0.0.1'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.9'
    compile group: 'com.nullables', name: 'json-rpc-lib-binding', version: '0.0.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.9'
}

test {
    reports {
        junitXml.enabled = true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'JSON-RPC Library Client'
                packaging 'jar'
                description 'JSON-RPC Library Client'
                url 'https://github.com/nullables/json-rpc-lib-client'

                scm {
                    connection 'scm:git:git://github.com/nullables/json-rpc-lib-client.git'
                    developerConnection 'scm:git:ssh://github.com/nullables/json-rpc-lib-client.git'
                    url 'https://github.com/nullables/json-rpc-lib-client'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mobratil'
                        name 'Martin Obr√°til'
                        email 'martin.obratil@icloud.com'
                        organization 'Nullables'
                        organizationUrl 'https://www.nullables.com'
                    }
                }
            }
        }
    }
}